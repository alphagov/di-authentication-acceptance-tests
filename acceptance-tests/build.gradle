
plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

def dependencyVersions = [
        junit_version: '5.8.2',
        cucumber_version: '7.3.2',
        axe_version: '3.0',
        json_version: '20220320',
        aws_sdk_version: "2.17.191"
]

dependencies {
    testImplementation "io.cucumber:cucumber-java:${dependencyVersions.cucumber_version}",
                       "io.cucumber:cucumber-junit:${dependencyVersions.cucumber_version}",
                       "org.seleniumhq.selenium:selenium-java:4.1.3",
                       "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit_version}"

    testImplementation group: 'com.deque', name: 'axe-selenium', version:"${dependencyVersions.axe_version}"
    testImplementation group: 'org.json', name: 'json', version:"${dependencyVersions.json_version}"
    testImplementation "software.amazon.awssdk:s3:${dependencyVersions.aws_sdk_version}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit_version}"
}

group = 'acceptance-tests'
version = '1.0.0'
description = 'acceptance-tests'

java.sourceCompatibility = JavaVersion.VERSION_16

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'uk.gov.di.test.acceptance', 'src/test/resources']
        }
    }
}